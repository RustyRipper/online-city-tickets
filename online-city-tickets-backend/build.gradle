plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.avast.gradle.docker-compose' version '0.14.2'
	id "com.diffplug.spotless" version "6.25.0"
}

group = 'org.pwr'
version = '0.0.1-SNAPSHOT'

spotless {
  format 'misc', {
    target '*.gradle', '.gitattributes', '.gitignore'
    trimTrailingWhitespace()
    indentWithTabs()
    endWithNewline()
  }
  java {
    googleJavaFormat('1.21.0').aosp().reflowLongStrings().skipJavadocFormatting()
    formatAnnotations()
  }
}

java {
	sourceCompatibility = '21'
}

sourceSets {
	integration {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	integrationImplementation.extendsFrom implementation
	integrationRuntimeOnly.extendsFrom runtimeOnly
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'

	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	compileOnly 'org.projectlombok:lombok'

	runtimeOnly 'org.postgresql:postgresql'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	developmentOnly 'org.springframework.boot:spring-boot-docker-compose'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	integrationImplementation 'org.springframework.boot:spring-boot-starter-test'
	integrationImplementation 'org.springframework.security:spring-security-test'
	integrationImplementation 'org.testcontainers:postgresql:1.19.8'
	integrationImplementation 'net.datafaker:datafaker:2.2.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integration.output.classesDirs
	classpath = sourceSets.integration.runtimeClasspath
	shouldRunAfter test

	useJUnitPlatform()

	testLogging {
		events "passed"
	}
}

check.dependsOn integrationTest
